// 应用插件
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def baseUrl = 'https://github.com/stanhe'
def siteUrl = baseUrl
def gitUrl = "${baseUrl}/AutoAddVersionCode"
def issueUrl = "${baseUrl}/AutoAddVersionCode/issues"

install {
    repositories {
        mavenInstaller {
            // This generates POM.xml with proper paramters
            pom.project {

                //添加项目描述
                name 'Gradle Plugin for Android'
                url siteUrl

                //设置开源证书信息
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                //添加开发者信息
                developers {
                    developer {
                        name 'stanhe'
                        email 'hshl4314@gmail.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection baseUrl
                    url siteUrl
                }
            }
        }

    }
}

task sourcesJar(type: Jar) {
    from 'src/main/groovy'
    exclude 'META-INF'
    classifier = 'sources' //classifier add the suffix to the file. autoaddversioncode-1.0.3-sources.jar
}

task versionJar(type: Jar) {
    from 'src/main/groovy'
    exclude 'META-INF'
}

groovydoc {
    includePrivate = true
    source = 'src/main/groovy'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives versionJar
    archives groovydocJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//配置上传Bintray相关信息
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven' // 上传到中央仓库的名称
        name = 'AutoAddVerionCode' // 上传到jcenter 的项目名称
        desc = 'Auto add gradle versionCode when release' // 项目描述
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        labels = ['gradle', 'plugin']
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
    }
}